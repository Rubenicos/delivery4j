allprojects {
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'

    repositories {
        mavenCentral()
    }

    dependencies {
        compileOnly 'org.jetbrains:annotations:24.1.0'
    }

    compileJava {
        options.encoding = 'UTF-8'
    }

    java {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
        withSourcesJar()
        withJavadocJar()
    }

    publishing {
        publications {
            maven(MavenPublication) {
                groupId project.group
                artifactId project.name
                version project.version
                from components.java
            }
        }
    }
}

subprojects {
    dependencies {
        implementation rootProject
    }
}

def javadocProjects= [
        ':',
        ':delivery4j-rabbitmq',
        ':delivery4j-redis',
        ':delivery4j-hikari'
]

tasks.register('allJavadoc', Javadoc) {
    source javadocProjects.collect { project(it).sourceSets.main.allJava }
    classpath = files(javadocProjects.collect { project(it).sourceSets.main.compileClasspath })
    options {
        links 'https://docs.oracle.com/en/java/javase/11/docs/api/'
        links 'https://rabbitmq.github.io/rabbitmq-java-client/api/current/'
        links 'https://javadoc.io/doc/redis.clients/jedis/'
        links 'https://javadoc.io/doc/com.zaxxer/HikariCP/'
    }
    destinationDir = file(project.buildDir.path + '/docs/all-javadoc')
}
