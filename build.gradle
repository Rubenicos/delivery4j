allprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'

    idea {
        module {
            downloadJavadoc = true
            downloadSources = true
        }
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        compileOnly 'org.jetbrains:annotations:26.0.1'
    }

    compileJava {
        options.encoding = 'UTF-8'
    }

    java {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
        withSourcesJar()
        withJavadocJar()
    }

    publishing {
        publications {
            maven(MavenPublication) {
                groupId project.group
                artifactId project.name
                version project.version
                from components.java
            }
        }
    }
}

subprojects {
    dependencies {
        implementation rootProject
    }
}

dependencies {
    testImplementation(platform('org.junit:junit-bom:5.11.3'))
    testImplementation 'org.junit.jupiter:junit-jupiter'

    testImplementation 'org.jetbrains:annotations:26.0.1'
}

test {
    useJUnitPlatform()
}

def javadocProjects= [
        ':',
        ':delivery4j-rabbitmq',
        ':delivery4j-redis',
        ':delivery4j-hikari',
        ':extension:extension-caffeine',
        ':extension:extension-guava',
        ':extension:extension-log4j',
        ':extension:extension-slf4j'
]

tasks.register('allJavadoc', Javadoc) {
    source javadocProjects.collect { project(it).sourceSets.main.allJava }
    classpath = files(javadocProjects.collect { project(it).sourceSets.main.compileClasspath })
    options {
        links 'https://docs.oracle.com/en/java/javase/11/docs/api/'
        links 'https://rabbitmq.github.io/rabbitmq-java-client/api/current/'
        links 'https://javadoc.io/doc/redis.clients/jedis/latest/'
        links 'https://javadoc.io/doc/com.zaxxer/HikariCP/latest/'
        links 'https://javadoc.io/doc/org.jetbrains/annotations/latest/'
    }
    destinationDir = file(project.layout.buildDirectory.dir('docs/all-javadoc'))
}
